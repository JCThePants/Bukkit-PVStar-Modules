defaultTasks 'build'

apply plugin: 'java'

// Minimum Java Version
sourceCompatibility = 1.7
targetCompatibility = 1.7


subprojects {

    project.evaluationDependsOnChildren()

    def projectName = null
    def majorVersion = '0'
    def buildBy = null
    def providedDepends = [
            'C:/JavaRepo/bukkit/bukkit-1.7.9-R0.jar',
            '../PV-StarAPI.jar',
            '../GenericsLib.jar'
    ]

    def compileDepends = null
    def includeFiles = null

    // Set version number
    if (ext.hasProperty("teamcity")) {
        version = majorVersion + '.' + ext.teamcity["build.number"] + '.git-' + ext.teamcity["build.vcs.number"]

        if (buildBy == null) {
            buildBy = ext.teamcity["build.triggeredBy.username"]

            if (!buildBy) {
                buildBy = ext.teamcity["build.triggeredBy"]
            }

            if (!buildBy) {
                buildBy = ext.teamcity["agent.name"]
            }
        }

    }else {
        version = majorVersion + '.000.git-unknown'
    }

    if (buildBy == null) {
        buildBy = System.properties['user.name']
    }


    repositories {
        mavenCentral()
    }

    // Add provided configuration to prevent dependencies
    // from being included in the jar
    configurations {
        provided
    }

    sourceSets {

        main {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided

            java {
                srcDir 'src'
            }
        }

        test {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
    }

    // Add dependencies
    dependencies {

        if (compileDepends) {
            compile files(compileDepends)
        }

        if (providedDepends) {
            provided files(providedDepends)
        }
    }


    // Jar file output with version number
    jar {

        archiveName = projectName + '.jar'

        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

        if (includeFiles) {
            from includeFiles
        }

        manifest {
            attributes("Built-By": buildBy,
                    "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
                    "Implementation-Title": projectName,
                    "Implementation-Version": version)
        }

    }

}