defaultTasks 'build'

apply plugin: 'java'

// Minimum Java Version
sourceCompatibility = 1.7
targetCompatibility = 1.7


subprojects {

    // Set version number
    if (ext.hasProperty("teamcity")) {
        version = project.majorVersion + '.' + ext.teamcity["build.number"] + '.git-' + ext.teamcity["build.vcs.number"]

        if (project.buildBy == null) {
            project.buildBy = ext.teamcity["build.triggeredBy.username"]

            if (!project.buildBy) {
                project.buildBy = ext.teamcity["build.triggeredBy"]
            }

            if (!project.buildBy) {
                project.buildBy = ext.teamcity["agent.name"]
            }
        }

    }else {
        version = project.majorVersion + '.000.git-unknown'
    }

    if (project.buildBy == null) {
        project.buildBy = System.properties['user.name']
    }




    repositories {
        mavenCentral()
    }

    // Add provided configuration to prevent dependencies
    // from being included in the jar
    configurations {
        provided
    }

    sourceSets {

        main {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided

            java {
                srcDir 'src'
            }
        }

        test {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
    }

    // Add dependencies
    dependencies {

        if (project.compileDepends) {
            compile files(project.compileDepends)
        }

        if (task.providedDepends) {
            provided files(project.providedDepends)
        }
    }


    // Jar file output with version number
    jar {

        archiveName = project.projectName + '.jar'

        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

        if (project.includeFiles) {
            from project.includeFiles
        }

        manifest {
            attributes("Built-By": project.buildBy,
                    "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
                    "Implementation-Title": projectName,
                    "Implementation-Version": version)
        }

    }

}